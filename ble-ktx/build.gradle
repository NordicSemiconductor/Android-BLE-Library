import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

android {
    namespace 'no.nordicsemi.android.ble.ktx'
    compileSdkVersion 36

    defaultConfig {
        minSdk 18
        targetSdk 36
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'ble-ktx-proguard-rules.pro'
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_17
        }
    }
}

dependencies {
    api project(':ble')
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'
}

// === Maven Central configuration ===
// The following file exists only when Android BLE Library project is opened, but not
// when the module is loaded to a different project.
if (rootProject.file('gradle/publish-module.gradle').exists()) {
    ext {
        POM_ARTIFACT_ID = 'ble-ktx'
        POM_NAME = 'Kotlin extension library for BLE Library for Android'
        POM_PACKAGING = 'aar'
    }
    apply from: rootProject.file('gradle/publish-module.gradle')
}

afterEvaluate {
    generateMetadataFileForReleasePublication.dependsOn androidSourcesJar
}
plugins {
    id 'com.android.library'
}

android {
    namespace 'no.nordicsemi.android.ble.livedata'
    compileSdk 35

    defaultConfig {
        minSdk 18
        targetSdk 35
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'ble-livedata-proguard-rules.pro'
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    api project(':ble')

    // https://developer.android.com/jetpack/androidx/releases/lifecycle
    api 'androidx.lifecycle:lifecycle-livedata:2.9.1'
}

// === Maven Central configuration ===
// The following file exists only when Android BLE Library project is opened, but not
// when the module is loaded to a different project.
if (rootProject.file('gradle/publish-module.gradle').exists()) {
    ext {
        POM_ARTIFACT_ID = 'ble-livedata'
        POM_NAME = 'A LiveData extension for the Bluetooth Low Energy library for Android'
        POM_PACKAGING = 'aar'
    }
    apply from: rootProject.file('gradle/publish-module.gradle')
}

afterEvaluate {
    generateMetadataFileForReleasePublication.dependsOn androidSourcesJar
}